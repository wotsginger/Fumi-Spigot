plugins {
    id 'java'
    id "xyz.jpenilla.run-paper" version "2.3.1"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = 'club.sitmc'
version = '1.0'

repositories {
    mavenCentral()
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.21.1-R0.1-SNAPSHOT")
    compileOnly("me.clip:placeholderapi:2.11.6")

    implementation("io.nats:jnats:2.16.8")
    implementation("org.json:json:20231013")
}

def targetJavaVersion = 21
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

// 修正后的 ShadowJar 配置
shadowJar {
    archiveClassifier.set('')
    // 将依赖打包进 Jar
    mergeServiceFiles()
}

// 关联任务
build.dependsOn(shadowJar)

runServer {
    minecraftVersion("1.21.1")
}